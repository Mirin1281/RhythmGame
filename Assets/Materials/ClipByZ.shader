Shader "Custom/ClipByZ"
{
    Properties
    {
        _MainTex ("Texture", 2D) = "white" {}
        _Color("Color", Color) = (1, 1, 1, 1)
        _ZThreshold ("Z Threshold", Float) = 0.0
        _InverseBlendRate ("BlendRate", Range(0.0, 1.0)) = 1
    }

    SubShader
    {
        Tags { "RenderType" = "Transparent" "Queue" = "Transparent" }
        Blend SrcAlpha OneMinusSrcAlpha 
        LOD 100

        Pass
        {
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag

            #include "UnityCG.cginc"

            struct appdata
            {
                float4 vertex : POSITION;
                float2 uv : TEXCOORD0;
            };

            struct v2f
            {
                float2 uv : TEXCOORD0;
                float4 vertex : SV_POSITION;
                float3 worldPos : TEXCOORD1;
            };

            sampler2D _MainTex;
            float4 _MainTex_ST;
            fixed4 _Color;
            float _ZThreshold;
            fixed _InverseBlendRate;

            v2f vert (appdata v)
            {
                v2f o;
                o.vertex = UnityObjectToClipPos(v.vertex);
                o.uv = TRANSFORM_TEX(v.uv, _MainTex);
                o.worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;
                return o;
            }

            fixed4 frag (v2f i) : SV_Target
            {
                // Z座標がしきい値より手前の場合は描画しない
                if (i.worldPos.y < _ZThreshold)
                {
                    discard;
                }
                fixed4 col = tex2D(_MainTex, i.uv) * _Color;
                col.a = _Color.a;
                col.r = abs(col.r - _InverseBlendRate);
                col.g = abs(col.g - _InverseBlendRate);
                col.b = abs(col.b - _InverseBlendRate);
                return col;
            }
            ENDCG
        }


        // 影のパスにも同じ処理を追加
        Pass
        {
            Tags { "LightMode" = "ShadowCaster" }

            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            #pragma multi_compile_shadowcaster

            #include "UnityCG.cginc"

            float _ZThreshold;
            fixed4 _ShadowColor;

            struct v2f
            {
                V2F_SHADOW_CASTER;
                float3 worldPos : TEXCOORD1;
            };

            v2f vert(appdata_full v)
            {
                v2f o;
                TRANSFER_SHADOW_CASTER(o)
                o.worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;
                return o;
            }

            fixed4 frag(v2f i) : SV_Target
            {
                if (i.worldPos.y < _ZThreshold)
                {
                    discard;
                }
                SHADOW_CASTER_FRAGMENT(i)
            }
            ENDCG
        }
    }
}